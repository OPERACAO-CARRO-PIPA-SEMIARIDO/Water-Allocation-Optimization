using JuMP
using LinearAlgebra
using CSV
using DataFrames
using Gurobi

include("../../utils/config_loader.jl")
config = load_config("config.json")
paths = config["paths"]
params = config["parameters"]

beneficiarios_ativos = CSV.read(paths["beneficiaries"], DataFrame)
dias_uteis = CSV.read(paths["dates"], DataFrame)
calendarios = CSV.read(paths["calendars"], DataFrame)
rotas = CSV.read(paths["routes"], DataFrame)

calendarioCarnaval = calendarios.carnaval
entregasObrigatorias = calendarios.lil

TOTAL_BENEFICIARIOS_ARQUIVO = get(params, "total_beneficiaries_in_file", size(beneficiarios_ativos, 1))
TOTAL_MANANCIAIS_ARQUIVO = get(params, "total_water_sources_in_file", 92)
CAPACIDADE_MAX_MANANCIAL = params["max_capacity_source"]
CAPACIDADE_CAMINHAO = get(params, "truck_capacity", 13.0)

TOTAL_BENEFICIARIOS = params["total_beneficiaries"]
TOTAL_MANANCIAIS = params["total_water_sources"]
TOTAL_DIAS = params["total_days"]
NUM_CANDIDATOS = params["num_candidates"]

nb = 1:TOTAL_BENEFICIARIOS
nd = 1:TOTAL_DIAS
nm = 1:TOTAL_MANANCIAIS

qtd_dias_uteis = sum(dias_uteis[nd, 1]) 
preU_full = [round(i * 0.02, digits=2) for i in beneficiarios_ativos.Pessoas_Atendidas]
U = preU_full[nb] 
C = convert(Vector{Float64}, beneficiarios_ativos.Capacidade)[nb]

Y = C ./ U
quebra4 = [j for (j, x) in zip(nb, Y) if x < 5]
quebra3 = [j for (j, x) in zip(nb, Y) if x < 4]
quebra2 = [j for (j, x) in zip(nb, Y) if x < 3]

distancias = rotas.distance_w_factor
Dij_completa = transpose(reshape(distancias, (TOTAL_BENEFICIARIOS_ARQUIVO, TOTAL_MANANCIAIS_ARQUIVO)))
Dij = Dij_completa[nm, nb]

CANDIDATOS_REAIS = min(NUM_CANDIDATOS, TOTAL_MANANCIAIS)
candidatos_por_beneficiario = Dict{Int, Vector{Int}}()
for j in nb
    fontes_ordenadas = sortperm(Dij[:, j])
    candidatos_por_beneficiario[j] = fontes_ordenadas[1:CANDIDATOS_REAIS]
end

function rodar_modelo_integrado(p::Float64, nome_pasta::String)
    caminho_pasta = joinpath(paths["results"], nome_pasta)
    mkpath(caminho_pasta)

    model = Model(Gurobi.Optimizer)
    
    set_optimizer_attribute(model, "NodefileStart", 10.0) 
    set_optimizer_attribute(model, "MemLimit", 28.0)
    set_optimizer_attribute(model, "Threads", get(params, "threads", 2))
    set_optimizer_attribute(model, "MIPGap", get(params, "mip_gap", 0.002))

    @variable(model, 0 <= x[j in nb, i in candidatos_por_beneficiario[j], k in nd], Int) 
    @variable(model, z[j in nb, i in candidatos_por_beneficiario[j]], Bin) 
    @variable(model, 0 <= y_pico, Int)
    @variable(model, 0 <= V[j in nb, k in 0:last(nd)])

    @expression(model, expr_pico, qtd_dias_uteis * y_pico)
    @expression(model, expr_custo, sum(Dij[i,j] * x[j, i, k] for j in nb, i in candidatos_por_beneficiario[j], k in nd))

    @objective(model, Min, (p * expr_pico) + ((1 - p) * expr_custo))

    @constraint(model, balancoVolumeInicial[j in nb], V[j, 0] == C[j])
    
    @constraint(model, balancoVolume[j in nb, k in 1:last(nd); !(calendarioCarnaval[k] == -1 && j in quebra4) && !(entregasObrigatorias[k] == -1 && j in quebra2)],
        V[j, k] <= V[j, k-1] - U[j] + CAPACIDADE_CAMINHAO * sum(x[j, i, k] for i in candidatos_por_beneficiario[j]))
    
    @constraint(model, correcaoVolume[j in nb, k in nd; (calendarioCarnaval[k] == -1 && j in quebra4) || (entregasObrigatorias[k] == -1 && j in quebra2)],
        V[j, k] == 0)
        
    @constraint(model, diasInuteis[j in nb, k in nd; Int(dias_uteis[k, 1]) == 0], sum(x[j, i, k] for i in candidatos_por_beneficiario[j]) == 0)
    
    @constraint(model, restMaiorPico[k in nd], sum(x[j, i, k] for j in nb, i in candidatos_por_beneficiario[j]) <= y_pico)
    
    @constraint(model, volumeMinimo[j in nb, k in 0:last(nd)], V[j, k] >= 0)
    @constraint(model, capacidadeMax[j in nb, k in 0:last(nd)], V[j, k] <= C[j])
    
    @constraint(model, carnavalAbastecimento[j in quebra4, k in nd; calendarioCarnaval[k] == 1], sum(x[j, i, k] for i in candidatos_por_beneficiario[j]) >= 1)
    @constraint(model, lilAbastecimento[j in quebra2, k in nd; entregasObrigatorias[k] == 1], sum(x[j, i, k] for i in candidatos_por_beneficiario[j]) >= 1)

    @constraint(model, fonteUnica[j in nb], sum(z[j, i] for i in candidatos_por_beneficiario[j]) == 1)
    @constraint(model, amarra_z_x[j in nb, k in nd, i in candidatos_por_beneficiario[j]], x[j, i, k] <= CAPACIDADE_MAX_MANANCIAL * z[j, i])
    @constraint(model, capDiariaManancial[i in nm, k in nd; !isempty([j for j in nb if i in candidatos_por_beneficiario[j]])],
        sum(x[j, i, k] for j in nb if i in candidatos_por_beneficiario[j]) <= CAPACIDADE_MAX_MANANCIAL)

    horas_checkpoints = get(params, "time_checkpoints_hours", [3, 6, 9, 12, 15, 18, 21, 24])
    tempo_acumulado = 0.0

    for meta_tempo in Float64.(horas_checkpoints .* 3600)
        tempo_restante = meta_tempo - tempo_acumulado
        if tempo_restante <= 0 continue end

        set_optimizer_attribute(model, "TimeLimit", tempo_restante)
        optimize!(model)
        tempo_acumulado = meta_tempo

        if has_values(model)
            salvar_saidas(model, caminho_pasta, "$(Int(meta_tempo/3600))h")
            salvar_saidas(model, caminho_pasta, "melhor_absoluto")
        end

        if termination_status(model) == MOI.OPTIMAL break end
    end
end

function salvar_saidas(model, pasta, sufixo)
    val_x = value.(model[:x])
    val_z = value.(model[:z])

    colunas_abastecimento = Any[[j for j in nb]]
    colunas_alocacao = Any[[j for j in nb]]

    for k in nd
        arr_abast_dia = Int[]
        arr_aloc_dia = Int[]
        for j in nb
            fonte_escolhida = 0
            for i in candidatos_por_beneficiario[j]
                if val_z[j, i] > 0.5
                    fonte_escolhida = i
                    break
                end
            end
            soma_caminhoes = sum(round(Int, val_x[j, i, k]) for i in candidatos_por_beneficiario[j])
            push!(arr_abast_dia, soma_caminhoes)
            push!(arr_aloc_dia, soma_caminhoes > 0 ? fonte_escolhida : 0)
        end
        push!(colunas_abastecimento, arr_abast_dia)
        push!(colunas_alocacao, arr_aloc_dia)
    end

    CSV.write(joinpath(pasta, "abastecimento_$sufixo.csv"), DataFrame(colunas_abastecimento, Symbol.(["Beneficiarios"; nd...])))
    CSV.write(joinpath(pasta, "alocacao_$sufixo.csv"), DataFrame(colunas_alocacao, Symbol.(["Beneficiarios"; nd...])))
end

if length(ARGS) >= 2
    rodar_modelo_integrado(parse(Float64, ARGS[1]), ARGS[2])
else
    p_def = get(params, "p_value", 0.1)
    println("Usando valor p padrão: $p_def")
    rodar_modelo_integrado(p_def, "resultado_full")
end
